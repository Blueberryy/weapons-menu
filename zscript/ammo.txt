class WM_AmmoEventHandler : EventHandler
{
  void MaybeDisableCrushing(Actor a)
  {
    bool disableCrushing = CVar.FindCVar("m8f_wm_DisableWeaponCrushing").GetInt();
    if (disableCrushing) { a.bDONTGIB = true; }
  }

  void MaybeDisableGibbing(Actor a)
  {
    bool disableGibbing = CVar.FindCVar("m8f_wm_DisableGibbing").GetInt();
    if (disableGibbing) { a.bNOEXTREMEDEATH = true; }
  }

  override void WorldThingSpawned(WorldEvent e)
  {
    Actor thing = e.thing;

    MaybeDisableGibbing(thing);

    Weapon w = Weapon(thing);
    if (w != null)
    {
      CVar  kickbackMultiplierCVar = CVar.FindCVar("m8f_wm_KickbackMultiplier");
      float kickbackMultiplier     = kickbackMultiplierCVar.GetFloat();
      w.Kickback *= kickBackMultiplier;

      CVar  bobMultiplierCVar = CVar.FindCVar("m8f_wm_BobMultiplier");
      float bobMultiplier     = bobMultiplierCVar.GetFloat();
      w.BobRangeX *= bobMultiplier;
      w.BobRangeY *= bobMultiplier;

      MaybeDisableCrushing(thing);
    }

    Inventory i = Inventory(thing);
    if (i == null) { return; }
    if (i.owner) { return; }

    Ammo a = Ammo(thing);
    if (a != null)
    {
      CVar  ammoMultiplierCVar = CVar.FindCVar("m8f_wm_AmmoMultiplier");
      float ammoMultiplier     = ammoMultiplierCVar.GetFloat();
      a.Amount *= ammoMultiplier;
      if (a.Amount == 0) { a.Amount = 1; }
    }
    else
    {
      Weapon w = Weapon(thing);
      if (w != null)
      {
        CVar  ammoMultiplierCVar = CVar.FindCVar("m8f_wm_AmmoMultiplier");
        float ammoMultiplier     = ammoMultiplierCVar.GetFloat();

        w.AmmoGive1 *= ammoMultiplier;
        w.AmmoGive2 *= ammoMultiplier;
        if (w.AmmoGive1 == 0 ) { w.AmmoGive1 = 1; }
        if (w.AmmoGive2 == 0 ) { w.AmmoGive2 = 1; }

        // spawn light
        CVar lightUpCVar = CVar.FindCVar("m8f_wm_LightNotAcquiredWeapons");
        bool lightUp     = lightUpCVar.GetInt();
        if (lightUp)
        {
          vector3 p = w.SpawnPoint;
          WM_Lighter light = WM_Lighter(Actor.Spawn("WM_Lighter", p));
          light.checkedWeapon = w.GetClassName();
        }
      }
    }
  }
}
