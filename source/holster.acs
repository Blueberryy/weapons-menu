#define N_MODS_NO_HOLSTER 1

script "WM_HolsterWeapon" (void)
{
  if (IsUninitialized()) { terminate; }

  int playerN = PlayerNumber();

  int selectedWeapon = WM_GetSelectedWeapon();
  if (selectedWeapon == WEAPON_HOLSTERED) { terminate; }

  if (WM_IsZoomed()) { WM_ToggleZoom(playerN, selectedWeapon); }

  int forbiddenMods[N_MODS_NO_HOLSTER] =
  {
    SET_WEAPONS_OF_SATURN, // reason: A_Lower with 0 duration
  };
  for (int i = 0; i < N_MODS_NO_HOLSTER; ++i)
  {
    if (WM_GetCurrentSet() == forbiddenMods[i])
    {
      Log(s:"Weapon holstering is not supported in this mod.");
      terminate;
    }
  }

  int holsterSpeedMultiplier = GetUserCvar(playerN, "M8fWeaponMenuHolsterSpeedMultiplier");
  if (holsterSpeedMultiplier != 0.0)
  {
    SetActorProperty(0, APROP_SPEED, holsterSpeedMultiplier);
  }

  bool fovChangeEnabled = GetUserCvar(playerN, "m8f_wm_HolsterFovEnabled");
  if (fovChangeEnabled)
  {
    WM_SetOriginalFov(WM_GetFov(playerN));
    int fovHolstered = GetUserCvar(playerN, "m8f_wm_HolsterFov");
    WM_SetFov(fovHolstered);
  }

  WM_ChangeState(STATE_CLOSED, playerN);
  WM_SetLastUsedWeapon(WM_GetWeaponClass(selectedWeapon), playerN);
  WM_SetSelectedWeapon(WEAPON_HOLSTERED, playerN);
  ScriptCall("WMZScriptHelper", "HolsterWeapon");
}
