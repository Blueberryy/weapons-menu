#library "LIBWEAPONMENU"
#include "zcommon.acs"

#include "data.acs"

#define messageIdStart   191919
#define msgIdWeapon      messageIdStart
#define msgIdSelected    messageIdStart + MAX_WEAPONS_NUMBER
#define msgIdHighlighted messageIdStart + MAX_WEAPONS_NUMBER + 1
#define msgIdError       messageIdStart + MAX_WEAPONS_NUMBER + 2
#define msgIdAmmo        messageIdStart + MAX_WEAPONS_NUMBER + 3
#define msgIdTag         messageIdStart + MAX_WEAPONS_NUMBER + 4

#define ERROR_NOT_FOUND  -1
// numbers [-1, -9] are reserved for error codes

#define WEAPON_HOLSTERED -10

// Global variables ////////////////////////////////////////////////////////////
// To write to global variables, use only special functions.

#define STATE_UNINITIALIZED  0
#define STATE_DISABLED       1
#define STATE_TERMINATED     2
#define STATE_ENABLED        3
#define STATE_CLOSED         4
#define STATE_OPENED         5
#define STATE_INVENTORY      6
#define STATE_CLOSED_VISIBLE 7
//
// (UNINITIALIZED) -titlemap-> [TERMINATED]
// (UNINITIALIZED) -unknown-class-> [DISABLED]
// (UNINITIALIZED) -known-class-and-always-open-> OPENED
// (UNINITIALIZED) -known-class-> CLOSED
//
// CLOSED -open->  OPENED
// OPENED -close-> CLOSED
// OPENED -close-and-always-open-> CLOSED_VISIBLE
//
// CLOSED -open-inventory-> INVENTORY
// OPENED -open-inventory-> INVENTORY
int globalState = 0;

// Global variables. Must be used only in enabled states.
int  currentSet;
int  weaponCount;
int  step;

int  highlightedWeapon;
int  selectedWeapon;
str  lastUsedWeapon;

bool selectedWatchEnabled = true;

bool isInInventory[MAX_WEAPONS_NUMBER];

int zoomFactor = 1.0;

// Inventory variables section /////////////////////////////////////////////////

int highlightedInventory = 0;
int highlightedInventoryClass;
int nInventoryItems;

#define N_INVENTORY_ITEMS 100

str inventoryClasses[N_INVENTORY_ITEMS];
str inventoryNames  [N_INVENTORY_ITEMS];

// Get status section //////////////////////////////////////////////////////////

function bool IsEnabled(void)    { return globalState >= STATE_ENABLED; }
function bool IsTerminated(void) { return globalState == STATE_TERMINATED; }
function bool IsOpened(void)     { return globalState == STATE_OPENED; }
function bool IsInventory(void)  { return globalState == STATE_INVENTORY; }
function bool IsVisible(void)    { return globalState == STATE_OPENED || globalState == STATE_CLOSED_VISIBLE; }

function bool IsAlwaysShow(void)
{
  int playerN     = PlayerNumber();
  bool alwaysShow = GetUserCVar(playerN, "M8fWeaponMenuAlwaysShow");
  return alwaysShow;
}

function int WM_GetInitialState(void)
{
  if (WM_IsTitlemap()) { return STATE_TERMINATED; }

  currentSet = WM_GetWeaponSet();
  if (currentSet == ERROR_NOT_FOUND) { return STATE_DISABLED; }

  return STATE_ENABLED;
}

// Global state must be changed only with this function.
function void WM_ChangeState(int newState)
{
  if (newState == globalState)
  {
    return;
  }

  WM_LeaveState(globalState);
  WM_EnterState(newState);
}

function void WM_LeaveState(int oldState)
{
  // leave previous state
  switch (oldState)
    {
    case STATE_UNINITIALIZED:  WM_LeaveUninitialized(); break;
    case STATE_DISABLED:       WM_LeaveDisabled();      break;
    case STATE_TERMINATED:     WM_LeaveTerminated();    break;
    case STATE_Enabled:        WM_LeaveEnabled();       break;
    case STATE_CLOSED:         WM_LeaveClosed();        break;
    case STATE_OPENED:         WM_LeaveOpened();        break;
    case STATE_INVENTORY:      WM_LeaveInventory();     break;
    case STATE_CLOSED_VISIBLE: WM_LeaveClosedVisible(); break;
    }
}

function void WM_EnterState(int newState)
{
  globalState = newState;
  WM_SetCVar(IsEnabled(), "M8fWeaponMenuIsEnabled");
  WM_SetCVar(IsOpened() || IsInventory(), "M8fWeaponMenuIsOpened");

  switch (newState)
    {
    case STATE_UNINITIALIZED:  WM_EnterUninitialized(); break;
    case STATE_DISABLED:       WM_EnterDisabled();      break;
    case STATE_TERMINATED:     WM_EnterTerminated();    break;
    case STATE_Enabled:        WM_EnterEnabled();       break;
    case STATE_CLOSED:         WM_EnterClosed();        break;
    case STATE_OPENED:         WM_EnterOpened();        break;
    case STATE_INVENTORY:      WM_EnterInventory();     break;
    case STATE_CLOSED_VISIBLE: WM_EnterClosedVisible(); break;
    }
}

function void WM_LeaveUninitialized(void)
{
}

function void WM_LeaveDisabled(void) {}
function void WM_LeaveTerminated(void) {}
function void WM_LeaveEnabled(void) {}
function void WM_LeaveClosed(void) {}

function void WM_LeaveOpened(void)
{
  WM_ClearScreen();
}

function void WM_LeaveInventory(void)
{
  WM_PlaySwitchSound();
  WM_ClearScreen();
}

function void WM_LeaveClosedVisible(void) {}

function void WM_EnterUninitialized(void) {}

function void WM_EnterDisabled(void)
{
  Log(s:"Weapon Menu: unknown player class: ",
      s:GetActorClass(0),
      s:", menu is disabled.\nYou can try 'wmauto' in console.");
}

function void WM_EnterTerminated(void) {}

function void WM_EnterEnabled(void)
{
  weaponCount = weaponCounts[currentSet];
  step        = WM_IfThenElse((weaponCount < 20), 0.05, 0.02);

  WM_SetSelectedWeapon(WM_FindRealSelectedWeapon());
  WM_SetHighlightedWeapon(selectedWeapon);

  int playerN = PlayerNumber();
  lastUsedWeapon = GetUserCVarString(playerN, "m8f_wm_LastUsedWeapon");
  if (!CheckInventory(lastUsedWeapon))
  {
    WM_SetLastUsedWeapon(WM_GetWeaponName(selectedWeapon));
  }

  WM_SetupAddons();
  WM_UpdateIsInInventory();
  WM_InitApiCvars();

  Log(s:"Weapon menu is loaded.");

  if (IsAlwaysShow()) { WM_ChangeState(STATE_CLOSED_VISIBLE); }
  else                { WM_ChangeState(STATE_CLOSED); }
}

function void WM_EnterClosed(void)
{
  if (highlightedWeapon != selectedWeapon)
  {
    WM_SelectWeapon(highlightedWeapon, true);
  }

  if (IsAlwaysShow())
  {
    WM_ChangeState(STATE_CLOSED_VISIBLE);
  }
}

function void WM_EnterOpened(void)
{
  WM_UpdateIsInInventory();
  WM_UpdateMenu();
}

function void WM_EnterInventory(void)
{
  WM_SetHighlightedWeapon(WM_FindRealSelectedWeapon());
  WM_PlaySwitchSound();
  WM_PrintInventoryMenu();
}

function void WM_EnterClosedVisible(void)
{
  WM_UpdateIsInInventory();
  WM_UpdateMenu();
}

function bool WM_IsHiddenWeapon(int wNumber)
{
  switch (currentSet)
  {

    // Smooth Doom Rifle
  case  1: return (wNumber == 2);

  case 13: return (wNumber == 5); // HXRTC Project: Omicron Raygun

    // Chex Quest: Super Large Zorcher is deathmatch-only
  case 19: return (wNumber == 3);

  case 25: return (wNumber == 2 || wNumber == 10 || wNumber == 11);
  case 28: return (wNumber == 3 || wNumber == 9);
  case 29: return (wNumber == 3 || wNumber == 9);
  case 30: return (wNumber == 3 || wNumber == 9);
  case 31: return (wNumber == 9 || wNumber == 10 || wNumber == 11);

    // Accessories to Murder
  case 41: return (wNumber == 2 || wNumber == 3);

    // DoomRL Arsenal
  case 42: return true;

    // High Noon Drifter:
  case 43:
    if (wNumber == 10) return true; // Double whips
    // Single SMG when Second SMG is acquired:
    if (wNumber ==  6 && WM_IsInInventory(5)) return true;
    if (wNumber ==  5) return true; // Double SMG
    return false;

    // X-Weapon
  case 45:
    if (wNumber == 27 || wNumber == 28) return true;
    if (wNumber ==  4 && WM_IsInInventory( 2)) return true;
    if (wNumber ==  8 && WM_IsInInventory( 7)) return true;
    if (wNumber == 11 && WM_IsInInventory(10)) return true;
    if (wNumber == 14 && WM_IsInInventory(13)) return true;
    if (wNumber == 17 && WM_IsInInventory(16)) return true;
    if (wNumber == 20 && WM_IsInInventory(19)) return true;
    if (wNumber == 24 && WM_IsInInventory(23)) return true;
    if (wNumber ==  2) return true;
    if (wNumber ==  3) return true;
    if (wNumber ==  7) return true;
    if (wNumber == 10) return true;
    if (wNumber == 13) return true;
    if (wNumber == 16) return true;
    if (wNumber == 19) return true;
    if (wNumber == 22) return true;
    return false;

    // Doom Delta
  case 47: return (wNumber == 8);

    // Factotum
  case 49:
    if (wNumber == 2 && WM_IsInInventory(1)) return true;
    if (wNumber == 6 && WM_IsInInventory(5)) return true;
    if (wNumber == 1) return true;
    if (wNumber == 5) return true;
    return false;

    // Flakes Doom
  case 50: return true; // TODO: make game detection and hide weapons properly

  }

  return false;
}


// Weapon data access functions ////////////////////////////////////////////////

function int WM_GetWeaponSlot(int weaponNumber)
{
  return slots[currentSet][weaponNumber];
}

function str WM_GetWeaponName(int weaponNumber)
{
  return names[currentSet][weaponNumber];
}

function str WM_GetPrintableWeaponName(int weaponNumber)
{
  return printableNames[currentSet][weaponNumber];
}

// Returns weaponNumber of next weapon in this slot, or -1 if there is no more
// weapons in this slot.
function int WM_GetWeaponSibling(int weaponNumber)
{
  int thisWeaponSlot = WM_GetWeaponSlot(weaponNumber);

  int playerN = PlayerNumber();
  int cycleValue = GetUserCVar(playerN, "M8fWMCycle");
  bool cycle = (cycleValue <= 1) && (cycleValue != 0);

  if (!cycle)
  {
    int next = weaponNumber + 1;
    if (next >= weaponCount) { return ERROR_NOT_FOUND; }
    int nextSlot = WM_GetWeaponSlot(next);
    if (nextSlot != thisWeaponSlot) { return ERROR_NOT_FOUND; }
    return next;
  }

  for (int i = 0; i < weaponCount; ++i)
  {
    int w = (weaponNumber + i) % weaponCount;
    if (w != weaponNumber && WM_GetWeaponSlot(w) == thisWeaponSlot)
    {
      return w;
    }
  }

  return ERROR_NOT_FOUND;
}

function int WM_GetWeaponBySlotInInv(int slotNumber)
{
  for (int i = 0; i < weaponCount; ++i)
  {
    if (slotNumber == WM_GetWeaponSlot(i))
      if (CheckInventory(WM_GetWeaponName(i)))
    {
      return i;
    }
  }

  return ERROR_NOT_FOUND;
}

function bool WM_IsOnlyInSlot(int weaponNumber)
{
  int slot = WM_GetWeaponSlot(weaponNumber);

  for (int prev = weaponNumber - 1; prev >= 0; --prev)
  {
    if (WM_GetWeaponSlot(prev) == slot)
      if (CheckInventory(WM_GetWeaponName(prev)))
        { return false; }
  }

  for (int next = weaponNumber + 1; next < weaponCount; ++next)
  {
    if (WM_GetWeaponSlot(next) == slot)
      if (CheckInventory(WM_GetWeaponName(next)))
        { return false; }
  }

  return true;
}

// Printing Weapons Menu functions /////////////////////////////////////////////

function void WM_UpdateMenu(void)
{
  WM_ClearScreen();
  if (IsVisible()) { WM_PrintMenu(); }
}

function void WM_ClearScreen(void)
{
  int id  = msgIdWeapon;
  int end = msgIdWeapon + MAX_WEAPONS_NUMBER;

  do // when menu is disabled, weapon count is 0, so always clear first message.
  {
    HudMessage(s:""; HUDMSG_PLAIN, id, 0, 0, 0, 0);
    ++id;
  } while (id < end);

  HudMessage(s:""; HUDMSG_PLAIN, msgIdSelected,    0, 0, 0, 0);
  HudMessage(s:""; HUDMSG_PLAIN, msgIdHighlighted, 0, 0, 0, 0);
}

function void WM_PrintMenu(void)
{
  int playerN = PlayerNumber();
  bool disableOutput = GetUserCVar(playerN, "M8fWeaponMenuDisableNativeOutput");
  if (disableOutput) { return; }

  if (!IsEnabled())
  {
    int stubXStart = GetUserCVar(playerN, "M8FWeaponMenuXStart");
    int stubYStart = GetUserCVar(playerN, "M8FWeaponMenuYStart");
    HudMessage(s:"Weapon Menu: \nUnrecognized weapon set.\nTo start Autodetection, type\nwmauto in console.";
               HUDMSG_PLAIN, msgIdError, CR_RED, stubXStart, stubYStart, 0);
    return;
  }

  WM_SetupFont();

  int availableWeaponColor    = GetUserCVar(playerN, "M8FAvailableWeaponColor");
  int notAvailableWeaponColor = GetUserCVar(playerN, "M8FNotAvailableWeaponColor");
  int highlightedWeaponColor  = GetUserCVar(playerN, "M8FHighlightedWeaponColor");
  int selectedWeaponColor     = GetUserCVar(playerN, "M8FSelectedWeaponColor");

  bool showNotAvailable  = GetUserCVar(playerN, "M8FShowNotAvailable");
  bool highToLow         = GetUserCVar(playerN, "M8FWeaponMenuHighToLow");
  bool numbersOnTheLeft  = GetUserCVar(playerN, "M8FWeaponMenuNumbersLeft");

  int  displayMode = GetUserCVar(playerN, "M8fWeaponMenuDisplayMode");
  int  ammoMode    = GetUserCVar(playerN, "M8fWeaponMenuShowAmmoCounts");

  int xStart         = GetUserCVar(playerN, "M8FWeaponMenuXStart");
  int yStart         = GetUserCVar(playerN, "M8FWeaponMenuYStart");
  int stepMultiplier = GetUserCVar(playerN, "M8FWeaponMenuStepMultiplier");

  int stepMultiplied = (step * stepMultiplier) >> 16;

  if (displayMode != 0)
  {
    if (selectedWeapon != WEAPON_HOLSTERED)
    {
      str selectedName = WM_GetPrintableWeaponName(selectedWeapon);
      HudMessage(s:"Selected: ", s:selectedName;
                 HUDMSG_PLAIN, msgIdSelected,
                 selectedWeaponColor, 0.5, yStart + stepMultiplied * 2, 0);
    }

    str highlightedName = WM_GetPrintableWeaponName(highlightedWeapon);
    str message = StrParam(s:"Highlighted: ", s:highlightedName);

    if (ammoMode != 0)
    {
      str highlightedClass = WM_GetWeaponName(highlightedWeapon);
      str ammo = WM_GetAmmoString(highlightedClass, ammoMode);
      message = StrParam(s:message, s:" ", s:ammo);
    }
    HudMessage(s:message;
               HUDMSG_PLAIN, msgIdHighlighted,
               highlightedWeaponColor, 0.5, yStart + stepMultiplied * 4, 0);
  }
  else
  {
    HudMessage(s:""; HUDMSG_PLAIN, msgIdSelected,    0, 0, 0, 0);
    HudMessage(s:""; HUDMSG_PLAIN, msgIdHighlighted, 0, 0, 0, 0);
  }

  int position          = 0;
  int availablePosition = -1;
  int holdingPosition   = -1;

  int iBegin;
  int iEnd;
  int iStep;
  if (highToLow) { iBegin = weaponCount - 1; iEnd = -1;          iStep = -1; }
  else           { iBegin = 0;               iEnd = weaponCount; iStep =  1; }

  for (int i = iBegin; i != iEnd; i += iStep)
  {
    // remember position of selected weapon
    if (i == highlightedWeapon) { availablePosition = position; }
    if (i == selectedWeapon)    { holdingPosition   = position; }

    if (WM_IsInInventory(i))
    {
      bool mustShow = (i != highlightedWeapon) || (i != selectedWeapon);

      if (mustShow)
      {
        WM_PrintHudWeapon(i,
                          position,
                          availableWeaponColor,
                          xStart,
                          yStart,
                          numbersOnTheLeft,
                          stepMultiplied,
                          displayMode,
                          ammoMode
        );
      }
      position += 1;
    }
    else if (showNotAvailable && !WM_IsHiddenWeapon(i))
    {
      WM_PrintHudWeapon(i,
                        position,
                        notAvailableWeaponColor,
                        xStart,
                        yStart,
                        numbersOnTheLeft,
                        stepMultiplied,
                        displayMode,
                        ammoMode
      );
      position += 1;
    }
  }

  if (WM_IsInInventory(highlightedWeapon))
  {
    WM_PrintHudWeapon(highlightedWeapon,
                      availablePosition,
                      highlightedWeaponColor,
                      xStart,
                      yStart,
                      numbersOnTheLeft,
                      stepMultiplied,
                      displayMode,
                      ammoMode
    );
  }

  if (selectedWeapon != WEAPON_HOLSTERED)
  {
    if (WM_IsInInventory(selectedWeapon) && selectedWeapon != highlightedWeapon)
    {
      WM_PrintHudWeapon(selectedWeapon,
                        holdingPosition,
                        selectedWeaponColor,
                        xStart,
                        yStart,
                        numbersOnTheLeft,
                        stepMultiplied,
                        displayMode,
                        ammoMode
                        );
    }
  }
}

function void WM_PrintHudWeapon(
  int weaponNumber,
  int position,
  int color,
  int xStart,
  int yStart,
  bool numbersOnTheLeft,
  int stepMultiplied,
  int displayMode,
  int ammoMode
)
{
  int id = msgIdWeapon + weaponNumber;
  int y = yStart;
  int x = xStart;
  if (displayMode == 2) { x += position * stepMultiplied; } // horizontal
  else                  { y += position * stepMultiplied; } // vertical

  int slotNumber = WM_GetWeaponSlot(weaponNumber);

  str weaponString;
  if (displayMode == 0) // with weapon names
  {
    str name = WM_GetPrintableWeaponName(weaponNumber);
    str ammo = WM_GetAmmoString(WM_GetWeaponName(weaponNumber), ammoMode);
    str delimiter  = " - ";

    if (numbersOnTheLeft)
    {
      weaponString = StrParam(i:slotNumber, s:delimiter, s:name);
      if (ammoMode != 0) {weaponString = StrParam(s:weaponString, s:" ", s:ammo);}
    }
    else
    {
      weaponString = StrParam(s:name, s:delimiter, i:slotNumber);
      if (ammoMode != 0) {weaponString = StrParam(s:ammo, s:" ", s:weaponString);}
    }
  }
  else // without weapon names
  {
    weaponString = StrParam(s:" ", i:slotNumber, s:" ");
  }

  HudMessage(s:" ", s:weaponString, s:" "; HUDMSG_PLAIN, id, color, x, y, 0);
}

#define N_FONTS 6

function void WM_SetupFont(void)
{
  str fonts[N_FONTS] =
  {
    "SMALLFONT",
    "CONFONT",
    "BIGFONT",
    "MINIPL_W",
    "MINIPLWK",
    "MM2SFNTO",
  };

  int playerN  = PlayerNumber();
  int iFont    = GetUserCVar(playerN, "M8fWeaponMenuFont");

  if (0 <= iFont && iFont < N_FONTS)
  {
    SetFont(fonts[iFont]);
  }
}


// Control functions section ///////////////////////////////////////////////////

// Must be called only from states that are IsEnabled().
function int WM_FindRealSelectedWeapon(void)
{
  return WM_FindInWeaponList(GetWeapon());
}

function int WM_FindInWeaponList(str weaponName)
{
  for (int i = 0; i < weaponCount; ++i)
  {
    if (StrCmp(weaponName, WM_GetWeaponName(i)) == 0)
    {
      return i;
    }
  }
  return ERROR_NOT_FOUND;
}

function void WM_SelectWeapon(int weaponNumber, bool changeLast)
{
  if (weaponNumber == selectedWeapon) { return; }
  if (WM_IsZoomed()) { WM_ToggleZoom(); }

  str  weaponName = WM_GetWeaponName(weaponNumber);
  bool success    = SetWeapon(weaponName);

  if (success)
  {
    int playerN = PlayerNumber();
    int holsterSpeedMultiplier = GetUserCvar(playerN, "M8fWeaponMenuHolsterSpeedMultiplier");
    if (holsterSpeedMultiplier != 0.0 && selectedWeapon == WEAPON_HOLSTERED)
    {
      SetActorProperty(0, APROP_SPEED, 1.0);
    }

    if (changeLast) { WM_SetLastUsedWeapon(WM_GetWeaponName(selectedWeapon)); }
    WM_SetSelectedWeapon(weaponNumber);
  }
  else
  {
    str printableWeaponName = WM_GetPrintableWeaponName(weaponNumber);
    HudMessage(s:printableWeaponName, s:": no ammo.";
               HUDMSG_PLAIN, msgIdAmmo, CR_RED, 0.5, 0.5, 1.0);

    WM_SetHighlightedWeapon(selectedWeapon);
  }
}

function void WM_InitApiCvars(void)
{
  int playerN     = PlayerNumber();

  SetUserCVar(playerN, "M8fWeaponMenuWeaponCount", weaponCount);
  SetUserCVar(playerN, "M8fWeaponMenuIsOpened", false);

  for (int i = 0; i < weaponCount; ++i) {
    str weaponClassVarName = StrParam(s:"M8fWeaponMenuWeaponClass", i:i);
    str weaponNameVarName  = StrParam(s:"M8fWeaponMenuWeaponName",  i:i);
    str weaponSlotVarName  = StrParam(s:"M8fWeaponMenuWeaponSlot",  i:i);

    SetUserCVarString(playerN, weaponClassVarName, WM_GetWeaponName(i));
    SetUserCVarString(playerN, weaponNameVarName,  WM_GetPrintableWeaponName(i));
    SetUserCVar      (playerN, weaponSlotVarName,  WM_GetWeaponSlot(i));
  }
}

function void WM_PlaySwitchSound(void)
{
  int playerN = PlayerNumber();
  int switchSound = GetUserCVar(playerN, "M8fWeaponMenuSwitchSound");

  if (switchSound)
  {
    str sound = StrParam(s:"wmenu/switch", i:switchSound);
    PlaySound(0, sound);
  }
}

function void WM_SetupAddons(void)
{
  WM_MaybeTacticalChainsaw();
  WM_MaybeUACSurvivalPack();
}

function void WM_AddWeapon(int slot, str class, str name)
{
  slots[currentSet][weaponCount] = slot;
  names[currentSet][weaponCount] = class;
  printableNames[currentSet][weaponCount] = name;
  ++weaponCount;
}

function void WM_MaybeUACSurvivalPack(void)
{
  str satchelChargeClass = "Det_satchelCharge";
  if (CheckClass(satchelChargeClass))
  {
    WM_AddWeapon(0, satchelChargeClass, "Satchel Charge with Detonator");
  }

  str pipebombClass = "Det_Pipebomb";
  if (CheckClass(pipebombClass))
  {
    WM_AddWeapon(0, pipebombClass, "Pipebombs with Detonator");
  }
}

function void WM_MaybeTacticalChainsaw(void)
{
  // function specifically for Tactical Chainsaw mod
  // https://www.doomworld.com/idgames/combos/ph_stuff

  str tacticalClass = "TacticalChainsaw";
  if (!CheckClass(tacticalClass)) { return; }

  for (int i = weaponCount; i > 0; --i)
  {
    WM_SwapWeapons(i, i-1);
  }
  slots[currentSet][0] = 0;
  names[currentSet][0] = tacticalClass;
  printableNames[currentSet][0] = "Tactical Chainsaw";
  ++weaponCount;
}

#define N_MATCHED_CLASSES 16

function int WM_SpecialMatchClassToWeaponSet(str class)
{
  // First case: one weapon set for several classes.

  str classes[N_MATCHED_CLASSES][2] =
  {
    // DoomRL Arsenal
    { "DoomRLMarine",        42 },
    { "DoomRLScout",         42 },
    { "DoomRLTechnician",    42 },
    { "DoomRLRenegade",      42 },
    { "DoomRLDemolitionist", 42 },
    { "DoomRLCommando",      42 },

    // X-Weapon
    { "XDoomPlayer",         45 },
    { "XDoomPlayer2",        45 },
    { "XDoomPlayer3",        45 },
    { "XDoomPlayer4",        45 },

    // Harmony mods
    { "HarmonyPlayer",       46 },

    // Doom Delta
    { "LoreleiPlayer", 47 },
    { "JohnPlayer",    47 },
    { "DimitriPlayer", 47 },
    { "ThiPlayer",     47 },
    { "BetaPlayer",    47 },
  };

  for (int i = 0; i < N_MATCHED_CLASSES; ++i)
  {
    if (StrCmp(class, classes[i][0]) == 0)
    {
      return classes[i][1];
    }
  }

  // Second case: one class for several weapon sets.

  if (StrCmp(class, "DoomPlayer") == 0)
  {
    if      (WM_IsFreedoom()) { return 44; }
    else if (WM_IsHarmony())  { return 46; }
    else                      { return  0; }
  }

  if (StrCmp(class, "PerkDPlayer") == 0)
  {
    if (WM_IsEVP()) { return 48; }
    else            { return  1; }
  }

  if (StrCmp(class, "FKPlayer") == 0)
  {
    if (WM_IsFlakesDoom()) { return 50; }
    else                   { return 14; }
  }

  return ERROR_NOT_FOUND;
}

function int WM_GetWeaponSet(void)
{
  int playerN      = PlayerNumber();
  str remClass     = GetUserCVarString(playerN, "M8fWeaponMenuRememberedClass");
  str currentClass = GetActorClass(0);

  // if class was previously wmauto'ed, it is current now.
  if (StrICmp(currentClass, remClass) == 0)
  {
    Log(s:"Weapon menu: loading autodetected weapon set...");
    WM_LoadRemembered();
    return AUTO_SET;
  }

  // search for class in special lists (class/set: one-to-many and many-to-one).
  int matchedClass = WM_SpecialMatchClassToWeaponSet(currentClass);
  if (matchedClass != ERROR_NOT_FOUND)
  {
    return matchedClass;
  }

  // search for class in regular lists (class/set: one-to-one).
  for (int c = 0; c < SETS_NUMBER; ++c)
  {
    if (CheckActorClass(0, playerClassNames[c]))
    {
      return c;
    }
  }

  return ERROR_NOT_FOUND;
}

function bool WM_IsTitlemap(void)
{
  str mapLumpName = StrParam(n:PRINTNAME_LEVEL);
  return (StrICmp(mapLumpName, "TITLEMAP") == 0);
}

function bool WM_IsFreedoom(void)
{
  str checkString = StrParam(l:"CC_IMP");
  return (StrICmp(checkString, "serpentipede") == 0);
}

function bool WM_IsHarmony(void)
{
  str checkString = StrParam(l:"C1TEXT");
  str harmString = "HEADING DOWN YOU FINALLY HAVE";
  int harmLength = StrLen(harmString);
  bool isHarmony = StrICmp(harmString, StrLeft(checkString, harmLength)) == 0;
  return isHarmony;
}

function bool WM_IsEVP(void)
{
  bool isEVP = CheckClass("EVPBFGBall");
  return isEVP;
}

function bool WM_IsFlakesDoom(void)
{
  bool isFlakesDoom = CheckClass("FKHAR");
  return isFlakesDoom;
}

function bool WM_IsOpenOnScroll(void)
{
  int playerN = PlayerNumber();
  return GetUserCVar(playerN, "M8fWeaponMenuOpenOnScroll");
}

function bool WM_IsMelee(void)
{
  if (selectedWeapon == WEAPON_HOLSTERED) return true;

  int playerN = PlayerNumber();
  int isSlot1Melee = !GetUserCVar(playerN, "m8f_wm_IsNotSlot1Melee");

  if (isSlot1Melee && WM_GetWeaponSlot(selectedWeapon) == 1) return true;
  return false;
}

// Scripts section /////////////////////////////////////////////////////////////

script "ShowWeaponMenu" ENTER
{
  int state = WM_GetInitialState();

  if (state == STATE_TERMINATED) { terminate; }

  WM_ChangeState(state);

  int updatePeriod = 10;
  int updateCount = 0;

  int playerN = PlayerNumber();
  int oldInstant = GetUserCVar(playerN, "M8fWeaponMenuInstantSwitch");

  // options
  while (true)
  {
    // CVar may have been reset by other games while this game was saved.
    WM_SetCVar(IsEnabled(), "M8fWeaponMenuIsEnabled");
    WM_SetCVar(IsOpened() || IsInventory(),  "M8fWeaponMenuIsOpened");

    if (updateCount < updatePeriod) { ++updateCount; }
    else
    {
      updateCount = 0;

      bool instant = GetUserCVar(playerN, "M8fWeaponMenuInstantSwitch");
      if (oldInstant != instant)
      {
        WM_InstantSwitch(instant == 2);
        oldInstant = instant;
      }

      if (IsVisible())
      {
        bool updated = WM_UpdateIsInInventory();
        if (updated) { WM_UpdateMenu(); }
      }

      if (IsInventory())
      {
        WM_UpdateInventoryMenu();
        WM_PrintInventoryMenu();
      }
    }

    /* debug output
    SetFont("SMALLFONT");
    Log(s:"sel:",   i:selectedWeapon,
        s:" high:", i:highlightedWeapon,
        s:" last:", s:lastUsedWeapon);
    ///*///

    // Check if currently selected weapon corresponds to what player is holding.
    // Handles situation when player drops a weapon, or switches to new weapon
    // on pickup.
    if (IsEnabled())
    {
      if (selectedWatchEnabled && !WM_IsSelected(selectedWeapon))
      {
        str currentWeaponClass = GetWeapon();
        int currentWeaponI     = WM_FindInWeaponList(currentWeaponClass);

        if (currentWeaponI != ERROR_NOT_FOUND)
        {
          str selectedClass = WM_GetWeaponName(selectedWeapon);
          if (CheckInventory(selectedClass))
          {
            WM_SetLastUsedWeapon(selectedClass);
          }

          WM_SetHighlightedWeapon(currentWeaponI);
          WM_SetSelectedWeapon(highlightedWeapon);
          WM_UpdateMenu();
        }
      }
    }

    bool showLaserSight = GetUserCVar(playerN, "m8f_wm_ShowLaserSight");
    if (showLaserSight && !WM_IsMelee())
    {
      WM_ShowLaserSight();
    }

    Delay(1);
  }
}

script "ApplyWeaponMenuSettings" (void)
{
  WM_UpdateMenu();
}

script "ToggleWeaponMenu" (int closeWithoutAction)
{
  if (IsInventory())
  {
    if (closeWithoutAction)
    {
      WM_ChangeState(STATE_CLOSED);
    }
    else
    {
      UseInventory(highlightedInventoryClass);
    }
    terminate;
  }

  if (IsOpened() && closeWithoutAction)
  {
    WM_SetHighlightedWeapon(WM_FindRealSelectedWeapon());
  }

  WM_ToggleWeaponMenu();
}

function void WM_ToggleWeaponMenu(void)
{
  if (IsOpened())
  {
    WM_ChangeState(STATE_CLOSED);
  }
  else
  {
    WM_ChangeState(STATE_OPENED);
  }
}

script "ScrollNextWeapon" (void)
{
  if (IsInventory())
  {
    int oldHighlighted = highlightedInventory;
    highlightedInventory = (highlightedInventory + 1) % nInventoryItems;
    if (oldHighlighted != highlightedInventory)
    {
      WM_PlaySwitchSound();
      highlightedInventoryClass = inventoryClasses[highlightedInventory];
    }
    WM_PrintInventoryMenu();
    terminate;
  }

  if (WM_IsOpenOnScroll() && !IsOpened())
  {
    WM_ChangeState(STATE_OPENED);
  }

  int i;
  int stopper = weaponCount;

  if (IsOpened())
  {
    i = highlightedWeapon;
    do
    {
      ++i;
      if (i >= weaponCount) { i = 0; }
      --stopper;
    }
    while (!WM_IsInInventory(i) && stopper);

    WM_SetHighlightedWeapon(i);
    if (stopper) { WM_PlaySwitchSound(); }
  }

  else
  {
    i = selectedWeapon;
    if (i == WEAPON_HOLSTERED) { i = 0; }
    do
    {
      ++i;
      if (i >= weaponCount) { i = 0; }
      --stopper;
    }
    while (!SetWeapon(WM_GetWeaponName(i)) && stopper);

    if (i != selectedWeapon)
    {
      WM_SetLastUsedWeapon(WM_GetWeaponName(selectedWeapon));
      WM_SetSelectedWeapon(i);
      WM_SetHighlightedWeapon(i);
      WM_MaybeDisplayNameTag();
    }
  }

  WM_UpdateMenu();
}

function void WM_MaybeDisplayNameTag(void)
{
  bool showNameTag = (GetCVar("displaynametags") >= 2);
  if (showNameTag)
  {
    int nametagcolor = GetCVar("nametagcolor");
    str selectedName = WM_GetPrintableWeaponName(selectedWeapon);
    HudMessage(s:selectedName;
               HUDMSG_FADEOUT, msgIdTag, nametagcolor, 0.5, 0.9, 2.0, 1.0);

  }
}

script "ScrollPreviousWeapon" (void)
{
  if (IsInventory())
  {
    int oldHighlighted = highlightedInventory;
    highlightedInventory = (highlightedInventory + nInventoryItems - 1)
      % nInventoryItems;
    if (oldHighlighted != highlightedInventory)
    {
      WM_PlaySwitchSound();
      highlightedInventoryClass = inventoryClasses[highlightedInventory];
    }
    WM_PrintInventoryMenu();
    terminate;
  }

  if (WM_IsOpenOnScroll() && !IsOpened())
  {
    WM_ToggleWeaponMenu();
  }

  int i;
  int stopper = weaponCount;

  if (IsOpened())
  {
    i = highlightedWeapon;
    do
    {
      --i;
      if (i < 0) { i = weaponCount - 1; }
      --stopper;
    }
    while (!WM_IsInInventory(i) && stopper);

    WM_SetHighlightedWeapon(i);
    if (stopper) { WM_PlaySwitchSound(); }
  }

  else
  {
    i = selectedWeapon;
    if (i == WEAPON_HOLSTERED) { i = weaponCount - 1; }
    do
    {
      --i;
      if (i < 0) { i = weaponCount - 1; }
      --stopper;
    }
    while (!SetWeapon(WM_GetWeaponName(i)) && stopper);

    if (i != selectedWeapon)
    {
      WM_SetLastUsedWeapon(WM_GetWeaponName(selectedWeapon));
      WM_SetSelectedWeapon(i);
      WM_SetHighlightedWeapon(i);
      WM_MaybeDisplayNameTag();
    }
  }

  int playerN = PlayerNumber();
  SetUserCVar(playerN, "M8fWeaponMenuHighlightedWeapon", highlightedWeapon);

  WM_UpdateMenu();
}

script "SmartWeaponSelection" (int slotNumber)
{
  if (IsInventory())
  {
    WM_ChangeState(STATE_CLOSED);
  }

  if (IsOpened())
  {
    int currentSlot = WM_GetWeaponSlot(highlightedWeapon);
    bool sameSlot = (slotNumber == currentSlot);
    if (sameSlot)
    {
      int sibling = highlightedWeapon;
      int stopper = weaponCount;
      do
      {
        sibling = WM_GetWeaponSibling(sibling);
        if (sibling == -1) break;
        --stopper;
      }
      while (!WM_IsInInventory(sibling) && stopper);

      if (sibling != -1 && sibling != highlightedWeapon)
      {
        WM_SetHighlightedWeapon(sibling);
        WM_PlaySwitchSound();
      }
    }
    else
    {
      WM_HighlightFirstInInventory(slotNumber);
    }
  }

  else
  {
    int playerN = PlayerNumber();
    bool immediatelySelect = GetUserCVar(playerN, "M8fWeaponMenuImmediatelySelectIfOnlyInSlot");
    if (immediatelySelect)
    {
      int weaponNumber = WM_GetWeaponBySlotInInv(slotNumber);
      if (weaponNumber == -1) { terminate; }

      if (WM_IsOnlyInSlot(weaponNumber))
      {
        if (CheckInventory(WM_GetWeaponName(weaponNumber)))
        {
          WM_SelectWeapon(weaponNumber, true);
          terminate;
        }
      }
    }

    WM_ToggleWeaponMenu();
    WM_HighlightFirstInInventory(slotNumber);
  }

  WM_UpdateMenu();
}

script "SelectLastUsedWeapon" (void)
{
  if (CheckInventory(lastUsedWeapon))
  {
    WM_SelectWeapon(WM_FindInWeaponList(lastUsedWeapon), true);
  }

  WM_UpdateMenu();
}

script "WM_MarkWeaponAsFavorite" (void)
{
  if (selectedWeapon == WEAPON_HOLSTERED) { terminate; }
  WM_SetCVar(selectedWeapon, "M8fWeaponMenuFavoriteWeapon");
  Log(s:WM_GetPrintableWeaponName(selectedWeapon), s:" is set as favorite weapon.");
}

script "WM_SelectFavoriteWeapon" (void)
{
  int playerN = PlayerNumber();
  int favorite = GetUserCVar(playerN, "M8fWeaponMenuFavoriteWeapon");
  if (CheckInventory(WM_GetWeaponName(favorite)))
  {
    WM_SelectWeapon(favorite, true);
  }

  WM_UpdateMenu();
}


// Utility functions section ///////////////////////////////////////////////////

int updateCounter = 0;

function bool WM_UpdateIsInInventory(void)
{
  ++updateCounter;
  if (updateCounter > 10)
  {
    updateCounter = 0;
    return true;
  }

  bool updated = false;

  for (int i = 0; i < weaponCount; ++i)
  {
    bool old = isInInventory[i];
    isInInventory[i] = CheckInventory(WM_GetWeaponName(i));
    if (old != isInInventory[i]) { updated = true; }
  }

  return updated;
}

function void WM_ResetIsInInventory(void)
{
  for (int i = 0; i < weaponCount; ++i)
  {
    isInInventory[i] = CheckInventory(WM_GetWeaponName(i));
  }
}

function bool WM_IsInInventory(int weaponNumber)
{
  return isInInventory[weaponNumber];
}

function bool WM_IsSelected(int weaponNumber)
{
  if (weaponNumber == WEAPON_HOLSTERED) { return true; }
  return CheckWeapon(WM_GetWeaponName(weaponNumber));
}

function void WM_HighlightFirstInInventory(int slotNumber)
{
  int weaponNumber = WM_GetWeaponBySlotInInv(slotNumber);
  if (WM_IsInInventory(weaponNumber) && highlightedWeapon != weaponNumber)
  {
    WM_SetHighlightedWeapon(weaponNumber);
    WM_PlaySwitchSound();
  }
}

function bool WM_IsValidWeaponNumber(int weaponNumber)
{
  return (0 <= weaponNumber && weaponNumber < weaponCount);
}

function int WM_IfThenElse(bool condition, int trueValue, int falseValue)
{
  if (condition)
  {
    return trueValue;
  }

  return falseValue;
}

// Global variables write access functions /////////////////////////////////////

function void WM_SetCVar(int value, str cvarName)
{
  int playerN = PlayerNumber();
  SetUserCVar(playerN, cvarName, value);
}

function void WM_SetHighlightedWeapon(int weaponNumber)
{
  if (weaponNumber == WEAPON_HOLSTERED) { return; }
  highlightedWeapon = weaponNumber;
  WM_SetCVar(highlightedWeapon, "M8fWeaponMenuHighlightedWeapon");
}

function void WM_SetSelectedWeapon(int weaponNumber)
{
  selectedWeapon = weaponNumber;
  WM_SetCVar(selectedWeapon, "M8fWeaponMenuSelectedWeapon");
}

function void WM_SetLastUsedWeapon(str weaponClass)
{
  lastUsedWeapon = weaponClass;
  int playerN = PlayerNumber();
  SetUserCVarString(playerN, "m8f_wm_LastUsedWeapon", lastUsedWeapon);
}


// Autodetection section ///////////////////////////////////////////////////////

script "WM_RememberWeapon" (int slot)
{
  str currentWeapon = GetWeapon();

  if (StrCmp(currentWeapon, "WMWeaponStub") == 0) { terminate; }

  int count = weaponCounts[AUTO_SET];
  if (count < MAX_WEAPONS_NUMBER)
  {
    slots[AUTO_SET][count] = slot;
    names[AUTO_SET][count] = currentWeapon;
    ++weaponCounts[AUTO_SET];
  }

  GiveInventory("WMInstaSwitcher", 1);
  SetWeapon("WMWeaponStub");
  TakeInventory(currentWeapon, 1);
}

script "WM_Remember_Start" (void)
{
  weaponCounts[AUTO_SET] = 0;
  GiveInventory("WMInstaSwitcher", 1);
  GiveInventory("WMWeaponStub", 1);
}

script "WM_Remember_Fin" (void)
{
  // save all remembered info
  int playerN = PlayerNumber();
  int autoCount = weaponCounts[AUTO_SET];
  str playerActorClass = GetActorClass(0);
  SetUserCVar(playerN, "M8fWeaponMenuRememberedCount", autoCount);
  SetUserCVarString(playerN, "M8fWeaponMenuRememberedClass", playerActorClass);

  for (int w = 0; w < autoCount; ++w)
  {
    str weaponClassVarName = StrParam(s:"M8fWeaponMenuRememberedName", i:w);
    str weaponSlotVarName  = StrParam(s:"M8fWeaponMenuRememberedSlot", i:w);

    SetUserCVarString(playerN, weaponClassVarName, names[AUTO_SET][w]);
    SetUserCVar      (playerN, weaponSlotVarName,  slots[AUTO_SET][w]);
  }

  Log(s:"weapon count: ", i:autoCount);

  Log(s:"Player Class: ", s:playerActorClass);
  int i;
  Log(s:"slots:");
  for (i = 0; i < autoCount; ++i) { Log(i:slots[AUTO_SET][i], s:", "); }
  Log(s:"weapon classes:");
  for (i = 0; i < autoCount; ++i) { Log(s:"\"", s:names[AUTO_SET][i], s:"\", "); }
  Log(s:"weapon names:");
  for (i = 0; i < autoCount; ++i) { Log(s:"\"", s:WM_BeautifyWeaponName(names[AUTO_SET][i]), s:"\", "); }
}

function void WM_LoadRemembered(void)
{
  int playerN = PlayerNumber();
  int autoCount = GetUserCVar(playerN, "M8fWeaponMenuRememberedCount");
  weaponCounts[AUTO_SET] = autoCount;

  for (int i = 0; i < autoCount; ++i)
  {
    str weaponClassVarName = StrParam(s:"M8fWeaponMenuRememberedName", i:i);
    str weaponSlotVarName  = StrParam(s:"M8fWeaponMenuRememberedSlot", i:i);

    names[AUTO_SET][i] = GetUserCVarString(playerN, weaponClassVarName);
    slots[AUTO_SET][i] = GetUserCVar      (playerN, weaponSlotVarName);
    printableNames[AUTO_SET][i] = WM_BeautifyWeaponName(names[AUTO_SET][i]);
  }
}

function str WM_BeautifyWeaponName(str weaponClassName)
{
  str result = weaponClassName;
  result = WM_RemovePrefix(result);
  result = WM_Replace(result, '_', ' ');
  result = WM_DivideCamelCase(result);
  result = WM_Capitalize(result);
  return result;
}

#define N_PREFIX 15

function str WM_RemovePrefix(str source)
{
  str classes[N_PREFIX] =
  {
    "Doom4Player",
    "Infantry",
    "Gunner",
    "Commander",
    "Demolition",
    "Marksman",
    "Recon",
    "Colonel",
    "Mercenary",
    "Heavy",
    "Elite",
    "Slipgater",
    "Samurai",
    "Pointman",
    "OMGDoomPlayer",
  };
  str prefix[N_PREFIX] =
  {
    "D4",
    "SZ_",
    "SZ_",
    "SZ_",
    "SZ_",
    "SZ_",
    "SZ_",
    "SZ_",
    "SZ_",
    "SZ_",
    "SZ_",
    "SZ_",
    "SZ_",
    "SZ_",
    "OMG_",
  };

  str currentClass = GetActorClass(0);

  for (int i = 0; i < N_PREFIX; ++i)
  {
    int prefixLength = StrLen(prefix[i]);
    if (StrCmp(currentClass, classes[i]) == 0 &&
        StrCmp(StrLeft(source, prefixLength), prefix[i]) == 0)
    {
      return StrRight(source, StrLen(source) - prefixLength);
    }
  }

  return source;
}

function str WM_Capitalize(str source)
{
  int firstChar = GetChar(source, 0);
  str result;
  if (WM_IsSmallLetter(firstChar))
  {
    int capitalized = firstChar - ('a' - 'A');
    int sourceLength = StrLen(source);
    result = StrParam(c:capitalized, s:StrRight(source, sourceLength - 1));
  }
  else
  {
    result = source;
  }
  return result;
}

function str WM_DivideCamelCase(str source)
{
  int sourceLength = StrLen(source);
  str result = "";
  str new;
  for (int i = 1; i < sourceLength; ++i)
  {
    int letter1 = GetChar(source, i-1);
    int letter2 = GetChar(source, i);
    if (WM_IsSmallLetter(letter1) && WM_IsBigLetter(letter2))
    {
      new = StrParam(s:result, c:letter1, s:" ");
      result = new;
    }
    else
    {
      new = StrParam(s:result, c:letter1);
      result = new;
    }
  }
  new = StrParam(s:result, s:StrRight(source, 1));
  result = new;

  return result;
}

function str WM_Replace(str source, int rem, int add)
{
  int sourceLength = StrLen(source);
  str result = "";
  str new;
  for (int i = 0; i < sourceLength; ++i)
  {
    int letter = GetChar(source, i);
    int replacement = WM_IfThenElse((letter == rem), add, letter);
    new = StrParam(s:result, c:replacement);
    result = new;
  }

  return result;
}

function bool WM_IsSmallLetter(int character)
{
  return ('a' <= character && character <= 'z');
}

function bool WM_IsBigLetter(int character)
{
  return ('A' <= character && character <= 'Z');
}

// Weapon Priority Section /////////////////////////////////////////////////////

script "WM_IncreasePriority" (void)
{
  str name = WM_GetPrintableWeaponName(highlightedWeapon);
  if (highlightedWeapon == 0)
  {
    Log(s:"Priority of ", s:name, s:" is already maximal.");
    terminate;
  }

  int oldMajorWeapon = highlightedWeapon - 1;
  int thisWeaponSlot = WM_GetWeaponSlot(highlightedWeapon);
  int oldWeaponSlot  = WM_GetWeaponSlot(oldMajorWeapon);
  if (thisWeaponSlot != oldWeaponSlot)
  {
    Log(s:"Priority of ", s:name, s:" is already maximal.");
    terminate;
  }

  WM_SwapWeapons(highlightedWeapon, oldMajorWeapon);
  WM_SetHighlightedWeapon(oldMajorWeapon);
  Log(s:"Priority of ", s:name, s:" is increased.");
  WM_UpdateMenu();
}

function void WM_SwapWeapons(int w1, int w2)
{
  if (w1 == w2) { return; }

  int namesTmp = names[currentSet][w1];
  names[currentSet][w1] = names[currentSet][w2];
  names[currentSet][w2] = namesTmp;

  int playerN = PlayerNumber();
  str weaponClassVarName1 = StrParam(s:"M8fWeaponMenuWeaponClass", i:w1);
  str weaponClassVarName2 = StrParam(s:"M8fWeaponMenuWeaponClass", i:w2);
  SetUserCVarString(playerN, weaponClassVarName1, WM_GetWeaponName(w1));
  SetUserCVarString(playerN, weaponClassVarName2, WM_GetWeaponName(w2));

  int printableNamesTmp = printableNames[currentSet][w1];
  printableNames[currentSet][w1] = printableNames[currentSet][w2];
  printableNames[currentSet][w2] = printableNamesTmp;

  str weaponNameVarName1 = StrParam(s:"M8fWeaponMenuWeaponName", i:w1);
  str weaponNameVarName2 = StrParam(s:"M8fWeaponMenuWeaponName", i:w2);
  SetUserCVarString(playerN, weaponNameVarName1, WM_GetPrintableWeaponName(w1));
  SetUserCVarString(playerN, weaponNameVarName2, WM_GetPrintableWeaponName(w2));

  int slotTmp = slots[currentSet][w1];
  slots[currentSet][w1] = slots[currentSet][w2];
  slots[currentSet][w2] = slotTmp;

  str slotVarName1 = StrParam(s:"M8fWeaponMenuWeaponSlot", i:w1);
  str slotVarName2 = StrParam(s:"M8fWeaponMenuWeaponSlot", i:w2);
  SetUserCVar(playerN, slotVarName1, WM_GetWeaponSlot(w1));
  SetUserCVar(playerN, slotVarName2, WM_GetWeaponSlot(w2));
}

script "WM_IncreaseSlot" (void)
{
  if (selectedWeapon == WEAPON_HOLSTERED) { terminate; }

  int currentSlot = WM_GetWeaponSlot(highlightedWeapon);
  if (currentSlot == 11) { terminate; }

  int nextSlot[11] = { 10, 2, 3, 4, 5, 6, 7, 8, 9, 0, 11 };
  int targetSlot = nextSlot[currentSlot];

  int i = highlightedWeapon;
  if (i != weaponCount - 1)
  {
    while ((WM_GetWeaponSlot(i + 1) == currentSlot) && (i != weaponCount - 1))
    {
      WM_SwapWeapons(i, i + 1);
      if (i + 1 == selectedWeapon) { WM_SetSelectedWeapon(i); }
      ++i;
    }
  }
  WM_ResetIsInInventory();

  slots[currentSet][i] = targetSlot;
  WM_SetHighlightedWeapon(i);
  str name = WM_GetPrintableWeaponName(i);
  Log(s:"Slot of ", s:name, s:" is changed to ", i:targetSlot, s:".");
  WM_UpdateMenu();
}

script "WM_DecreaseSlot" (void)
{
  if (selectedWeapon == WEAPON_HOLSTERED) { terminate; }

  int currentSlot = WM_GetWeaponSlot(highlightedWeapon);
  if (currentSlot == 1) { terminate; }

  int prevSlot[12] = { 9, 0, 1, 2, 3, 4, 5, 6, 7, 8, 0, 10 };
  int targetSlot = prevSlot[currentSlot];

  int i = highlightedWeapon;
  if (i != 0)
  {
    while (WM_GetWeaponSlot(i - 1) == currentSlot && i != 0)
    {
      WM_SwapWeapons(i, i - 1);
      if (i - 1 == selectedWeapon) { WM_SetSelectedWeapon(i); }
      --i;
    }
  }
  WM_ResetIsInInventory();

  slots[currentSet][i] = targetSlot;
  str name = WM_GetPrintableWeaponName(highlightedWeapon);
  WM_SetHighlightedWeapon(i);
  Log(s:"Slot of ", s:name, s:" is changed to ", i:targetSlot, s:".");
  WM_UpdateMenu();
}

script "WM_PrintInfo" (void)
{
  Log(s:"Player: ", s:GetActorClass(0));
  Log(s:"Selected: ", i:selectedWeapon, s:": ",
      s:WM_GetWeaponName(selectedWeapon), s:": ",
      s:GetWeapon());
  Log(s:"Highlighted: ", i:highlightedWeapon, s:": ",
      s:WM_GetWeaponName(highlightedWeapon));
  Log(s:"Last used: ", s:lastUsedWeapon);
  int playerN = PlayerNumber();
  int favorite = GetUserCVar(playerN, "M8fWeaponMenuFavoriteWeapon");
  Log(s:"Favorite: ", i:favorite, s:": ",
      s:WM_GetWeaponName(favorite));
  Log(s:"Speed: ", f:GetActorProperty(0, APROP_Speed));
  Log(s:"State: ", i:globalState);
}

script "WM_ResetAutodetection" (void)
{
  int playerN = PlayerNumber();
  SetUserCVarString(playerN, "M8fWeaponMenuRememberedClass", "");
}

function str WM_GetAmmoString(str weaponClass, int ammoMode)
{
  if (ammoMode == 0) { return ""; }

  bool showSecondary = ammoMode == 2;
  int playerN = PlayerNumber();
  ScriptCall("WMZscriptHelper", "TellAmmo", weaponClass, showSecondary);
  return GetUserCVarString(playerN, "M8fWeaponMenuAmmoInformerResult");
}

function bool WM_IsWeaponReady(str weaponClass)
{
  int playerN = PlayerNumber();
  ScriptCall("WMZscriptHelper", "IsWeaponReady", weaponClass);
  bool isReady = GetUserCVar(playerN, "M8fWeaponMenuWeaponReady");
  return isReady;
}

function bool WM_IsWeaponDeselectable(str weaponClass)
{
  int playerN = PlayerNumber();
  ScriptCall("WMZscriptHelper", "IsWeaponDeselectable", weaponClass);
  bool isReady = GetUserCVar(playerN, "M8fWeaponMenuWeaponReady");
  return isReady;
}

script "WM_FireFavoriteWeapon" (void)
{
  if (selectedWeapon == WEAPON_HOLSTERED) { terminate; }

  int playerN = PlayerNumber();
  int favorite = GetUserCVar(playerN, "M8fWeaponMenuFavoriteWeapon");
  str favoriteClass = WM_GetWeaponName(favorite);
  if (!CheckInventory(favoriteClass)) { terminate; }

  bool returnToLast = (favorite != selectedWeapon);
  int returnTo = selectedWeapon;
  selectedWatchEnabled = false;

  int fireMode = GetUserCVar(playerN, "m8f_wm_FavoriteFireMode");
  bool instant = (GetUserCVar(playerN, "M8fWeaponMenuInstantSwitch") == 1);
  if (instant) { WM_InstantSwitch(true); }
  WM_SelectWeapon(favorite, false);
  if (instant) { Delay(1); WM_InstantSwitch(false); }

  Delay(10);

  bool isReady = WM_IsWeaponReady(favoriteClass);
  int i = 0;
  int iMax = 35 * 2; // 2 seconds
  bool alreadyFired = false;
  while (!(WM_IsSelected(favorite) && isReady))
  {
    // this is to interrupt script in case something has gone wrong.
    if (i > 0 && isReady && !WM_IsSelected(favorite))
    {
      selectedWatchEnabled = true;
      terminate;
    }
    ++i;

    Delay(1);
    isReady = (WM_IsWeaponReady(favoriteClass) || i > iMax);

    if (alreadyFired == false)
    {
      int buttons = GetPlayerInput(-1, INPUT_BUTTONS);
      alreadyFired = (buttons & (BT_ATTACK|BT_ALTATTACK));
    }
  }

  if (alreadyFired) { Delay(1); }
  else
  {
    switch (fireMode)
    {
    case 0: ScriptCall("WMZScriptHelper", "FireWeapon",    favoriteClass); break;
    case 1: ScriptCall("WMZScriptHelper", "FireWeaponAlt", favoriteClass); break;
    case 2: break;
    }
  }

  isReady = WM_IsWeaponDeselectable(favoriteClass);
  while (!isReady)
  {
    Delay(1);
    isReady = WM_IsWeaponDeselectable(favoriteClass);
  }

  if (returnToLast && CheckInventory(WM_GetWeaponName(returnTo)))
  {
    if (instant) { WM_InstantSwitch(true); }
    WM_SelectWeapon(returnTo, false);
    if (instant) { Delay(1); WM_InstantSwitch(false); }
  }

  selectedWatchEnabled = true;
}

function void WM_InstantSwitch(bool enable)
{
  SetPlayerProperty(0, enable, PROP_INSTANTWEAPONSWITCH);
}

#define N_MODS_NO_HOLSTER 2

script "WM_HolsterWeapon" (void)
{
  if (selectedWeapon == WEAPON_HOLSTERED) { terminate; }
  if (WM_IsZoomed()) { WM_ToggleZoom(); }

  int forbiddenMods[N_MODS_NO_HOLSTER] = { 16, 47 };
  for (int i = 0; i < N_MODS_NO_HOLSTER; ++i)
  {
    if (currentSet == forbiddenMods[i])
    {
      Log(s:"Weapon holstering is not supported in this mod.");
      terminate;
    }
  }

  int playerN = PlayerNumber();
  int holsterSpeedMultiplier = GetUserCvar(playerN, "M8fWeaponMenuHolsterSpeedMultiplier");
  if (holsterSpeedMultiplier != 0.0)
  {
    SetActorProperty(0, APROP_SPEED, holsterSpeedMultiplier);
  }

  WM_ChangeState(STATE_CLOSED);
  WM_SetLastUsedWeapon(WM_GetWeaponName(selectedWeapon));
  WM_SetSelectedWeapon(WEAPON_HOLSTERED);
  ScriptCall("WMZScriptHelper", "HolsterWeapon");
}

// Laser sight section /////////////////////////////////////////////////////////

#define N_PUFFS 6

str puffs[N_PUFFS] =
{
  "WM_Laser5",
  "WM_Laser0",
  "WM_Laser1",
  "WM_Laser2",
  "WM_Laser3",
  "WM_Laser4",
};

function void WM_ShowLaserSight(void)
{
  int angle = GetActorAngle(0);
  int pitch = GetActorPitch(0);
  int distanceCheckTid = UniqueTID();
  int maxDistance = 4096.0;
  LineAttack(0, angle, pitch, 0, "WM_InvisiblePuff", "None", maxDistance,
             FHF_NORANDOMPUFFZ | FHF_NOIMPACTDECAL, distanceCheckTid);
  int distanceX = GetActorX(distanceCheckTid);
  int distanceY = GetActorY(distanceCheckTid);
  int myX = GetActorX(0);
  int myY = GetActorY(0);
  int distanceXDelta = (distanceX - myX) >> 22;
  int distanceYDelta = (distanceY - myY) >> 22;
  int distance = distanceXDelta * distanceXDelta +
    distanceYDelta * distanceYDelta;

  int iPuff = 0;
  if      (distance <  18) { iPuff = 1; }
  else if (distance < 100) { iPuff = 2; }
  else if (distance < 300) { iPuff = 3; }
  else if (distance < 900) { iPuff = 4; }
  else                     { iPuff = 5; }

  if (zoomFactor >= 1.8) { --iPuff; }

  str puff = puffs[iPuff];

  /*
  Log(i:distance);
  Log(s:puff);
  ///*///

  LineAttack(0, angle, pitch, 0, puff, "None", maxDistance,
             FHF_NORANDOMPUFFZ | FHF_NOIMPACTDECAL);
}

// Inventory menu section //////////////////////////////////////////////////////

script "WM_InventoryMenu" (void)
{
  if (IsInventory())
  {
    WM_ChangeState(STATE_CLOSED);
  }
  else
  {
    WM_UpdateInventoryMenu();
    if (nInventoryItems != 0)
    {
      WM_ChangeState(STATE_INVENTORY);
    }
    else
    {
      Log(s:"Inventory is empty.");
    }
  }
}

function void WM_UpdateInventoryMenu(void)
{
  ScriptCall("WMZScriptHelper", "GetInventoryList");

  int playerN = PlayerNumber();
  str inventoryContents = GetUserCVarString(playerN, "M8fWeaponMenuAmmoInformerResult");

  int inventoryContentsLength = StrLen(inventoryContents);

  int current = 0;
  str currentToken = "";
  int iArray = 0;

  int prevHighlightedIndex = -1;
  for (int i = 0; i < inventoryContentsLength && current < N_INVENTORY_ITEMS; ++i)
  {
    int character = GetChar(inventoryContents, i);
    if (character != '>')
    {
      currentToken = StrParam(s:currentToken, c:character);
    }
    else
    {
      switch (iArray)
      {
      case 0:
        inventoryClasses[current] = currentToken;
        if (StrCmp(highlightedInventoryClass, currentToken) == 0)
        {
          prevHighlightedIndex = current;
        }
        break;
      case 1: inventoryNames[current] = currentToken; ++current; break;
      }

      iArray = (iArray + 1) % 2;
      currentToken = "";
    }
  }

  /*
  for (int j = 0; j < current; ++j)
  {
    Log(s:inventoryClasses[j],
        s:" : ", s:inventoryNames[j],
        s:" : ", s:inventoryAmounts[j]);
  }
  ///*///

  nInventoryItems = current;

  if (nInventoryItems != 0)
  {
    if (prevHighlightedIndex == -1)
    {
      highlightedInventory = 0;
      highlightedInventoryClass = inventoryClasses[highlightedInventory];
    }
    else
    {
      highlightedInventory = prevHighlightedIndex;
    }
  }
}

function void WM_PrintInventoryMenu(void)
{
  int playerN = PlayerNumber();
  bool disableOutput = GetUserCVar(playerN, "M8fWeaponMenuDisableNativeOutput");
  if (disableOutput) { return; }

  WM_SetupFont();

  int availableColor    = GetUserCVar(playerN, "M8FAvailableWeaponColor");
  int highlightedColor  = GetUserCVar(playerN, "M8FHighlightedWeaponColor");
  int displayMode       = GetUserCVar(playerN, "M8fWeaponMenuDisplayMode");
  int xStart            = GetUserCVar(playerN, "M8FWeaponMenuXStart");
  int yStart            = GetUserCVar(playerN, "M8FWeaponMenuYStart");
  int stepMultiplier    = GetUserCVar(playerN, "M8FWeaponMenuStepMultiplier");
  bool highToLow        = GetUserCVar(playerN, "M8FWeaponMenuHighToLow");

  int stepMultiplied = (step * stepMultiplier) >> 16;

  int iBegin;
  int iEnd;
  int iStep;
  if (highToLow) {iBegin = nInventoryItems-1; iEnd = -1;              iStep = -1;}
  else           {iBegin = 0;                 iEnd = nInventoryItems; iStep =  1;}

  int position = 0;
  for (int i = iBegin; i != iEnd; i += iStep)
  {
    int color;
    if (i == highlightedInventory) { color = highlightedColor; }
    else                           { color = availableColor;   }

    WM_PrintInventoryItem(i, position, color, xStart, yStart, stepMultiplied);
    ++position;
  }
}

function void WM_PrintInventoryItem(
  int i,
  int position,
  int color,
  int xStart,
  int yStart,
  int stepMultiplied
)
{
  int id = msgIdWeapon + i;
  int y = yStart + position * stepMultiplied;
  int x = xStart;

  str itemString = StrParam(s:inventoryNames[i]);
  HudMessage(s:" ", s:itemString, s:" "; HUDMSG_PLAIN, id, color, x, y, 0);
}

// Zoom Section ////////////////////////////////////////////////////////////////

script "WM_ToggleZoom" (void)
{
  WM_ToggleZoom();
}

function void WM_ToggleZoom(void)
{
  if (WM_IsMelee()) { return; }
  if (!WM_IsWeaponReady(WM_GetWeaponName(selectedWeapon))) { return; }

  int  playerN = PlayerNumber();
  bool isLaserConnected = GetUserCVar(playerN, "m8f_wm_LaserOnZoom");

  bool isZoomed = WM_IsZoomed();
  if (isZoomed) { zoomFactor = 1.0; }
  else          { zoomFactor = GetUserCVar(playerN, "m8f_wm_ZoomFactor"); }

  ScriptCall("WMZScriptHelper", "SetZoomFactor", zoomFactor);

  if (isLaserConnected)
  {
    SetUserCVar(playerN, "m8f_wm_ShowLaserSight", WM_IsZoomed());
  }
}

function bool WM_IsZoomed(void)
{
  return zoomFactor != 1.0;
}
