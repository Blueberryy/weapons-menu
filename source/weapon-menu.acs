#library "LIBWEAPONMENU"
#include "zcommon.acs"

#define MINIMAL_HOLD_TIME (1873)
#define messageIdStart (191919)

// Known weapon sets:
// 0 - Vanilla (Standard) Weapons
// 1 - PerK's Smoother Weapon Animations Enhanced
// 2 - Eriguns1 Standard
// 3 - Eriguns1 Extended
// 4 - Eriguns2
// 5 - Beautiful Doom
// 6 - Final Doomer - Plutonia
// 7 - Final Doomer - TNT
// 8 - Final Doomer - Doom 2
// 9 - Final Doomer - Ancient Aliens
// 10 - Final Doomer - JPCP
// 11 - Final Doomer - BTSX
// 12 - Faspons
// 13 - HXRTC Project
// 14 - Kriegsland
// 15 - Complex Doom V27a3
// 16 - Weapons of Saturn - October 31, 2017
// 17 - Hard-Doom v8.1.1
// 18 - Heretic
// 19 - Chex Quest (Single Player)

#define SETS_NUMBER (20)
#define MAX_WEAPONS_NUMBER (50)

str playerClassNames[SETS_NUMBER] =
{
  "DoomPlayer",        // Standard
  "PerkDPlayer",       // PerK's Smoother Weapon Animations Enhanced
  "EriPlayer", "AltPlayer", "HellPlayer", // Eriguns
  "BD_DoomPlayer",     // Beautiful Doom
  "FDPlutPlayer", "FDTNTPlayer", "FDDoom2Player", "FDAliensPlayer", "FDJPCPPlayer", "FDBTSXPlayer", // Final Doomer
  "FasPlayer",         // Faspons
  "HXRTCPlayer",       // HXRTC Project
  "FKPlayer",          // Kriegsland
  "ComplexDoomPlayer", // Complex Doom
  "SaturnPlayer",      // Weapons of Saturn
  "DoomMarine",        // Hard-Doom
  "HereticPlayer",     // Heretic
  "ChexPlayer",        // Chex Quest
};

int weaponCounts[SETS_NUMBER] =
{
  9,  // Standard
  9,  // PerK's Smoother Weapon Animations Enhanced
  9, 13, 13, // Eriguns
  9,  // Beautiful Doom
  9, 9, 9, 9, 8, 9, // Final Doomer: Plutonia, TNT, Doom2, AA, JPCP, BTSX
  9,  // Faspons
  50, // HXRTC Project!
  22 + 5, // Kriegsland
  20, // Complex Doom
  11, // Weapons of Saturn
  14, // Hard-Doom
  8,  // Heretic
  8,  // Chex Quest
};

int slots[SETS_NUMBER][MAX_WEAPONS_NUMBER] =
{
  { 1, 1, 2, 3, 3, 4, 5, 6, 7 }, // Standard

  { 1, 1, 2, 3, 3, 4, 5, 6, 7 }, // PerK's Smoother Weapon Animations Enhanced

  // Eriguns
  { 1, 1, 2, 3, 3, 4, 5, 6, 7 },
  { 1, 1, 2, 3, 3, 3, 4, 4, 5, 5, 6, 7, 8 },
  { 1, 1, 2, 2, 3, 4, 5, 6, 6, 7, 8, 8, 9 },

  { 1, 1, 2, 3, 3, 4, 5, 6, 7 }, // Beautiful Doom

  // Final Doomer
  { 1, 1, 2, 3, 3, 4, 5, 6, 7 },
  { 1, 1, 2, 3, 3, 4, 5, 6, 7 },
  { 1, 1, 2, 3, 3, 4, 5, 6, 7 },
  { 1, 1, 2, 3, 3, 4, 5, 6, 7 },
  { 1, 2, 3, 3, 4, 5, 6, 7 },
  { 1, 1, 2, 3, 3, 4, 5, 6, 7 },

  { 1, 1, 2, 3, 3, 4, 5, 6, 7 }, // Faspons

  { // HXRTC Project
    1, 1, 1, 1, 1, 1,
    2, 2, 2, 2, 2, 2,
    3, 3, 3, 3, 3,
    4, 4, 4, 4, 4, 4,
    5, 5, 5, 5, 5,
    6, 6, 6, 6, 6,
    7, 7, 7, 7, 7,
    8, 8, 8, 8, 8,
    9, 9, 9, 9, 9,
    0, 0
  },

  { // Kriegsland
    1, 1, 1,
    2, 2, 2,
    3, 3,
    4, 4,
    5, 5, 5,
    6, 6, 6,
    7, 7, 7,
    8, 8, 8,
    9, 9, 9, 9, 9, // cross akimbos
  },

  { // Complex Doom
    1, 1, 1, 1,
    2,
    3, 3, 3, 3,
    4, 4,
    5, 5,
    6, 6, 6,
    7, 7, 7,
    8,
  },

  { 1, 1, 2, 2, 3, 3, 4, 4, 5, 6, 7 }, // Weapons of Saturn

  { 1, 1, 2, 3, 3, 3, 4, 4, 5, 5, 6, 6, 7, 7 }, // Hard-Doom

  { 1, 1, 2, 3, 4, 5, 6, 7 }, // Heretic

  { 1, 1, 2, 3, 4, 5, 6, 7 }, // Chex Quest
};

str names[SETS_NUMBER][MAX_WEAPONS_NUMBER] =
{
  { "Chainsaw", "Fist", "Pistol", "SuperShotgun", "Shotgun", "Chaingun", "RocketLauncher", "PlasmaRifle", "BFG9000" },
  { "Z86Chainsaw", "PerkFist", "PerkPistol", "PerkSuperShotgun", "PerkShotgun", "Z86Chaingun", "PerkRocketLauncher", "BloxPlasmaRifle", "Z86BFG9000" },

  { "EriChainsaw", "EriFist", "EriPistol", "EriSuperShotgun", "EriShotgun", "EriMachinegun", "EriRocketLauncher", "EriPlasmaRifle", "EriBFG9000" },
  { "EriChainsaw", "EriFist", "AltPistol", "EriAutoShotgun", "AltSuperShotgun", "EriShotgun", "EriGatlingGun", "EriMachinegun", "EriMissileLauncher", "EriRocketLauncher", "EriPlasmaRifle", "EriBFG9000", "EriGrenadeLauncher" },
  { "EriBlade", "EriFist", "EriSuperImmolator", "EriImmolator", "EriBloodbane", "EriNailgun", "EriMauler", "EriHellforge", "EriIrebolt", "EriCaster", "EriHydra", "EriBasilisk", "EriSoulreaver" },

  { "BD_Chainsaw", "BD_Fist", "BD_Pistol", "BD_SuperShotgun", "BD_Shotgun", "BD_Chaingun", "BD_RocketLauncher", "BD_PlasmaRifle", "BD_BFG9000" },

  { "FDPlutChainsaw", "FDPlutFist", "FDPlutPistol", "FDPlutSuperShotgun", "FDPlutShotgun", "FDPlutChaingun", "FDPlutRocketLauncher", "FDPlutPlasmaRifle", "FDPlutBFG9000" },
  { "FDTNTChainsaw", "FDTNTFist", "FDTNTPistol", "FDTNTSuperShotgun", "FDTNTShotgun", "FDTNTChaingun", "FDTNTRocketLauncher", "FDTNTPlasmaRifle", "FDTNTBFG9000" },
  { "FDDoom2Chainsaw", "FDDoom2Fist", "FDDoom2Pistol", "FDDoom2SuperShotgun", "FDDoom2Shotgun", "FDDoom2Chaingun", "FDDoom2RocketLauncher", "FDDoom2PlasmaRifle", "FDDoom2BFG9000" },
  { "FDAliensChainsaw", "FDAliensFist", "FDAliensPistol", "FDAliensSuperShotgun", "FDAliensShotgun", "FDAliensChaingun", "FDAliensRocketLauncher", "FDAliensPlasmaRifle", "FDAliensBFG9000" },
  { "FDJPCPFist", "FDJPCPPistol", "FDJPCPSuperShotgun", "FDJPCPShotgun", "FDJPCPChaingun", "FDJPCPRocketLauncher", "FDJPCPPlasmaRifle", "FDJPCPBFG9000" },
  { "FDBTSXChainsaw", "FDBTSXFist", "FDBTSXPistol", "FDBTSXSuperShotgun", "FDBTSXShotgun", "FDBTSXChaingun", "FDBTSXRocketLauncher", "FDBTSXPlasmaRifle", "FDBTSXBFG9000" },

  { "Motorsaw", "Knuckles", "CombatRifle", "DoubleBarrel", "Riotgun", "Minigun", "RPG", "PulseRifle", "BFG" },

  {
    "Z-Gun", "DoomChainsaw", "Axe", "Knife", "DoubleKnuckles", "OmicronRaygun",
    "LightMachinegun", "MP40", "DualColt1911", "Colt1911", "DualBerettas", "Beretta",
    "W-Shotgun", "AutoShotgun", "QuadShotgun", "CoachGun", "RiotShotgun",
    "HeavyChaingun", "M134Minigun", "DemonkillerCarbine", "M24SniperRifle", "DualCombatRifles", "CombatRifle",
    "MultiElementalCannon", "CryogenicRepeater", "NapalmLauncher", "SawThrower", "Saturn-V",
    "GatlingLasergun", "DualPulseRifles", "PulseRifle", "DualPlasmaZappers", "PlasmaZapper",
    "BlackHoleMaker", "BFG-X", "RailgunV20", "NeedleBuster", "ProtoBlaster",
    "ChillyIceBombard", "FlameCannon", "ChillerCrossbow", "NightmareDoubleRaygun", "HellMissileLauncher",
    "AcidArbalest", "ToxicWand", "EvilWarriorShield", "UACChemicalSpray", "BlowGun",
    "LandMineWeapon", "HandGrenadeWeapon",
  },

  { // Kriegsland
    "HDGrenades", "HDChainsaw", "HDKnife",
    "HDBlaster", "HDHandgun", "HDPistol",
    "HDSuperShotgun", "HDShotgun",
    "HDChaingun", "HDMachinegun",
    "HDPanzerfaust", "HDFlareGun", "HDRocketLauncher",
    "HDSonicGun", "HDLightningGun", "HDPlasmaRifle",
    "HDLaserGun", "HDLurdinha", "HDBFG9000",
    "HDCarbine", "HDGewehr", "HDRifle",
    "HDCrossAkimbo5", "HDCrossAkimbo4", "HDCrossAkimbo3", "HDCrossAkimbo2", "HDCrossAkimbo1",
  },

  { // Complex Doom
    "Land Mine", "Hand Grenade", "Chainsaw ", "Melee",
    "Handgun",
    "Quad Barrel Shotgun", "Double Barrel Shotgun", "Assault Shotgun", "Combat Shotgun",
    "Minigun ", "Assault Rifle",
    "Grenade Launcher", "Rocket Launcher",
    "Railgun ", "Demon Tech Rifle", "Plasma Rifle",
    "Demon Tech BFG10K", "BFG10K ", "BFG9500",
    "MP40",
  },

  { // Weapons of Saturn
    "Motorsaw", "Knuckles",
    "1911", "Beretta",
    "Coachgun", "Riotgun",
    "Minigun", "MachineRifle",
    "SaturnV",
    "NovaRifle",
    "TITAN",
  },

  { // Hard-Doom
    "Chainsaw~", "Fist~",
    "Pistol~",
    "Quad-Shotgun", "SuperShotgun~", "Shotgun~",
    "Minigun~", "Chaingun~",
    "GrenadeLauncher~", "RocketLauncher~",
    "PlasmaRifle~", "Sonic Railgun",
    "BFG10K~", "BFG9000~",
  },

  { // Heretic
    "Gauntlets", "Staff",
    "GoldWand",
    "Crossbow",
    "Blaster",
    "SkullRod",
    "PhoenixRod",
    "Mace",
  },

  { // Chex Quest
    "SuperBootspork",
    "Bootspoon",
    "MiniZorcher",
    "LargeZorcher",
    "RapidZorcher",
    "ZorchPropulsor",
    "PhasingZorcher",
    "LAZDevice",
  },
};

str printableNames[SETS_NUMBER][MAX_WEAPONS_NUMBER] =
{
  { "Chainsaw", "Fist", "Pistol", "Super Shotgun", "Shotgun", "Chaingun", "Rocket Launcher", "Plasma Rifle", "BFG9000" },
  { "Chainsaw", "Fist", "Pistol", "Super Shotgun", "Shotgun", "Chaingun", "Rocket Launcher", "Plasma Rifle", "BFG9000" },

  { "Chainsaw", "Fist", "Pistol", "Super Shotgun", "Shotgun", "Chaingun", "Rocket Launcher", "Plasma Rifle", "BFG9000" },
  { "Chainsaw", "Fist", "Pistol", "Auto Shotgun", "Super Shotgun", "Shotgun", "Gatling Gun", "Machinegun", "Missile Launcher", "Rocket Launcher", "Plasma Rifle", "BFG9000", "Grenade Launcher" },
  { "Vorpal Blade", "Fist", "Super Immolator", "Immolator", "Bloodbane", "Nailgun", "Mauler", "Hellforge", "Irebolt", "Soul Caster", "Hydra", "Basilisk", "Soul Reaver" },

  { "Chainsaw", "Fist", "Pistol", "Super Shotgun", "Shotgun", "Chaingun", "Rocket Launcher", "Plasma Rifle", "BFG9000" },

  { "Power Saw", "Melee", "Ol' Faithful", "Super Shotgun", "Scoped Shotgun", "Machine Gun", "Grenade Launcher", "Heavy Machine Gun", "Quantum Accelerator" },
  { "Halderman Device", "Melee", "Prototype Pistol", "Burst Shotgun", "Tactical Shotgun", "Dual Uzis", "Missile Launcher", "Maser", "Incursion Annihilator" },
  { "Chainsaw", "Melee", "Pistol", "Super Shotgun", "Shotgun", "Chaingun", "Rocket Launcher", "Plasma Rifle", "BFG 9000" },
  { "Stasis Bow", "Trench Knife", "Ancient Pistol", "Hybrid SSG", "Ancient Shotgun", "Ancient AK", "Photon Scepter", "Alien Elite Weapon", "Illuminator" },
  { "Katana", "Silenced Pistol", "Neutron Cascader", "Neutron Pulser", "Burst Needler", "Prism Cannon", "Ofuda Caster", "Onmyou Devastator" },
  { "NanoCore", "Power Fist", "Charge Pistol", "Machreaper", "Arc Blaster", "Pulse Rifle", "Pulse Cannon", "Plasma Vulcan", "X-Spark Cannon" },

  { "Chainsaw", "Knuckles", "Combat Rifle", "Double Barrel Shotgun", "Riotgun", "Minigun", "RPG", "Pulse Rifle", "BFG9000" },

  {
    "Z-Gun", "Chainsaw", "Hatchet", "Knife", "Double Knuckles", "Omicron Raygun",
    "Light Machinegun", "MP40", "Dual Colt 1911", "Colt 1911", "Dual Berettas", "Beretta",
    "W-Shotgun", "Auto Shotgun", "Quad Shotgun", "Coach Gun", "Riot Shotgun",
    "Heavy Chaingun", "M134 Minigun", "Demonkiller Carbine", "M24 Sniper Rifle", "Dual Combat Rifles", "Combat Rifle",
    "Multi-Elemental Cannon", "Cryogenic Repeater", "Napalm Launcher", "Saw Thrower", "Saturn-V",
    "Gatling Lasergun", "Dual Pulse Rifles", "Pulse Rifle", "Dual Plasma Zappers", "Plasma Zapper",
    "Black Hole Maker", "BFG-X", "Railgun 2.0", "Needle Buster", "Proto Blaster",
    "Chilly Mancubus' Ice Bombard", "Mancubi's Flame Cannon", "Arctic Archer's Chiller Crossbow", "Nightmare Reaper's Double Raygun", "Skeleton's Hell Missile Launcher",
    "Acid Arbalest", "Toxic Wand", "Evil Warrior's Shield", "UAC Chemical Spray", "Poison Dart Blowgun",
    "Land Mine", "Hand Grenade",
  },

  { // Kriegsland
    "Stielhandgranate-68", "Elektroshocker", "Kampfmesser 84",
    "DL-44 FuturPistole-Blaster", "Mauser M972-S2", "Luger P-58",
    "Jadtfaust Wunderfilnte-8", "Luftwaffe DP-65",
    "MP-86s", "StG-60",
    "Panzerkreuz Mk. III", "Kampfpistole", "Lawine-6",
    "Schallkraftwerk", "Blitzgewehr", "Flammenfaust",
    "Ekelhaftwaffe", "MG-92", "VFR-245 Puls-Kanone",
    "Bombenschuss", "Gewehr-1953", "KAR 82a-2K",
    "Cross Thermals", "Cross Launchers", "Cross Machineguns", "Cross Shotguns", "Cross Pistols",
  },

  { // Complex Doom
    "Land Mine", "Hand Grenade", "Chainsaw", "Melee",
    "Handgun",
    "Quad Barrel Shotgun", "Double Barrel Shotgun", "Assault Shotgun", "Combat Shotgun",
    "Minigun", "Assault Rifle",
    "Grenade Launcher", "Rocket Launcher",
    "Railgun", "Demon Tech Rifle", "Plasma Rifle",
    "Demon Tech BFG10K", "BFG10K", "BFG9500",
    "MP40",
  },

  { // Weapons of Saturn
    "Chain Saw", "Knuckles",
    "1911", "Beretta",
    "Coach Gun", "Riot Gun",
    "Rotary Cannon", "Machine Rifle",
    "Saturn V",
    "Nova Rifle",
    "Titan",
  },

  { // Hard-Doom
    "Chainsaw", "Fist",
    "Pistol",
    "Quad-Shotgun", "Super Shotgun", "Shotgun",
    "Minigun", "Chaingun",
    "Grenade Launcher", "Rocket Launcher",
    "Plasma Rifle", "Sonic Railgun",
    "BFG10K", "BFG9000",
  },

  { // Heretic
    "Gauntlets of the Necromancer", "Staff",
    "Elven Wand",
    "Ethereal Crossbow",
    "Dragon Claw",
    "Hellstaff",
    "Phoenix Rod",
    "Firemace",
  },

  { // Chex Quest
    "Super Bootspork", "Spoon",
    "Mini Zorcher",
    "Large Zorcher",
    "Rapid Zorcher",
    "Zorch Propulsor",
    "Phasing Zorcher",
    "LAZ Device",
  },
};

bool menuIsOpened   = false;
int  selectedWeapon = 0;
int  currentSet     = -1;

function int getWeaponSlot(int weaponNumber)
{
  if (0 <= weaponNumber && weaponNumber < weaponCounts[currentSet])
  {
    return slots[currentSet][weaponNumber];
  }

  return 0;
}

function str getWeaponName(int weaponNumber)
{
  if (0 <= weaponNumber && weaponNumber < weaponCounts[currentSet])
  {
    return names[currentSet][weaponNumber];
  }

  return "UnknownWeapon";
}

function str getPrintableWeaponName(int weaponNumber)
{
  if (0 <= weaponNumber && weaponNumber < weaponCounts[currentSet])
  {
    return printableNames[currentSet][weaponNumber];
  }

  return "Unknown weapon";
}

// Returns weaponNumber of next weapon in this slot, or -1 if there is no
function int getWeaponSibling(int weaponNumber)
{
  int thisWeaponSlot = getWeaponSlot(weaponNumber);
  int weaponCount    = weaponCounts[currentSet];

  for (int i = 0; i < weaponCount; ++i)
  {
    int w = (weaponNumber + i) % weaponCount;
    if (w != weaponNumber && getWeaponSlot(w) == thisWeaponSlot)
    {
      return w;
    }
  }

  return -1;
}

function int getWeaponBySlot(int slotNumber)
{
  int weaponCount = weaponCounts[currentSet];

  for (int i = 0; i < weaponCount; ++i)
  {
    if (slotNumber == getWeaponSlot(i) && CheckInventory(GetWeaponName(i)))
    {
      return i;
    }
  }

  return -1;
}

function void printHudWeapon(int weaponNumber, int color, int xStart, int yStart, bool highToLow)
{
  int playerN = PlayerNumber();
  int id = messageIdStart + weaponNumber;

  int step;
  if (weaponCounts[currentSet] < 20)
  {
    step = 0.05;
  }
  else
  {
    step = 0.02;
  }

  int x = xStart;
  int y = yStart;
  if (highToLow)
  {
    y += (weaponCounts[currentSet] - 1 - weaponNumber) * step;
  }
  else
  {
    y += weaponNumber * step;
  }

  int slotNumber = getWeaponSlot(weaponNumber);
  str delimiter  = ". ";
  str name       = getPrintableWeaponName(weaponNumber);

  HudMessage(i:slotNumber, s:delimiter, s:name;
             HUDMSG_PLAIN,
             id, color, x, y, MINIMAL_HOLD_TIME);
}

function void SetSelected(void)
{
  int weaponCount = weaponCounts[currentSet];

  for (int s = 0; s < weaponCount; ++s)
  {
    if (checkWeapon(GetWeaponName(s)))
    {
      selectedWeapon = s;
      break;
    }
  }
}

script "ShowWeaponMenu" ENTER
{
  // check if player class is known
  for (int c = 0; c < SETS_NUMBER; ++c)
  {
    if (CheckActorClass(0, playerClassNames[c]))
    {
      currentSet = c;
      break;
    }
  }
  bool menuIsEnabled = (currentSet != -1);

  int playerN = PlayerNumber();

  SetUserCVar(playerN, "M8FWeaponMenuIsEnabled", menuIsEnabled);

  if (!menuIsEnabled)
  {
    log(s:"Weapon Menu: unknown player class: ",
        s:GetActorClass(0),
        s:", menu is disabled.");
    terminate;
  }

  SetSelected();

  int weaponCount = weaponCounts[currentSet];

  while (true)
  {
    if (menuIsOpened)
    {
      // can happen if player drops weapon, or pistol starter script is in action
      if (!CheckInventory(GetWeaponName(selectedWeapon)))
      {
        SetSelected();
      }

      int availableWeapon    = GetUserCVar(playerN, "M8FAvailableWeaponColor");
      int notAvailableWeapon = GetUserCVar(playerN, "M8FNotAvailableWeaponColor");
      int highlightedWeapon  = GetUserCVar(playerN, "M8FHighlightedWeaponColor");

      int xStart = getUserCVar(playerN, "M8FWeaponMenuXStart");
      int yStart = getUserCVar(playerN, "M8FWeaponMenuYStart");
      bool highToLow = getUserCVar(playerN, "M8FWeaponMenuHighToLow");

      for (int i = 0; i < weaponCount; ++i)
      {
        if (CheckInventory(GetWeaponName(i)))
        {
          printHudWeapon(i, availableWeapon, xStart, yStart, highToLow);
        }
        else
        {
          printHudWeapon(i, notAvailableWeapon, xStart, yStart, highToLow);
        }
      }

      printHudWeapon(selectedWeapon, highlightedWeapon, xStart, yStart, highToLow);

    }

    Delay(1);
  }
}

script "ToggleWeaponMenu" (void)
{
  ToggleWeaponMenuFunction();
  if (menuIsOpened)
  {
    SetSelected();
  }
}

function void ToggleWeaponMenuFunction(void)
{
  menuIsOpened = !menuIsOpened;

  // on close - select weapon in menu.
  if (!menuIsOpened)
  {
    SetWeaponByNumber(selectedWeapon);
  }
}

script "ScrollNextWeapon" (void)
{
  int weaponCount = weaponCounts[currentSet];

  if (menuIsOpened)
  {
    do
    {
      selectedWeapon += 1;
      if (selectedWeapon >= weaponCount)
      {
        selectedWeapon = 0;
      }
    }
    while (!CheckInventory(GetWeaponName(selectedWeapon)));
  }
  else
  {
    do
    {
      selectedWeapon += 1;
      if (selectedWeapon >= weaponCount)
      {
        selectedWeapon = 0;
      }
    }
    while (!SetWeapon(GetWeaponName(selectedWeapon)));
  }
}

script "ScrollPreviousWeapon" (void)
{
  int weaponCount = weaponCounts[currentSet];

  if (menuIsOpened)
  {
    do
    {
      selectedWeapon -= 1;
      if (selectedWeapon < 0)
      {
        selectedWeapon = weaponCount;
      }
    }
    while (!CheckInventory(GetWeaponName(selectedWeapon)));
  }
  else
  {
    do
    {
      selectedWeapon -= 1;
      if (selectedWeapon < 0)
      {
        selectedWeapon = weaponCount;
      }
    }
    while (!SetWeapon(GetWeaponName(selectedWeapon)));
  }
}

function void SetWeaponByNumber(int weaponNumber)
{
  str weaponName = GetWeaponName(weaponNumber);
  bool success = SetWeapon(weaponName);

  if (!success)
  {
    str printableWeaponName = GetPrintableWeaponName(weaponNumber);
    print(s:printableWeaponName, s:": no ammo.");
  }
}

function void SetSelectedIfAvailable(int newSelected)
{
  if (CheckInventory(GetWeaponName(newSelected)))
  {
    selectedWeapon = newSelected;
  }
}

script "SmartWeaponSelection" (int slotNumber)
{
  if (slotNumber >= weaponCounts[currentSet]) { terminate; }

  if (!menuIsOpened)
  {
    SetSelectedIfAvailable(getWeaponBySlot(slotNumber));
    ToggleWeaponMenuFunction(); // open menu
  }
  else
  {
    int currentSlot = getWeaponSlot(selectedWeapon);
    bool sameSlot = (slotNumber == currentSlot);

    if (sameSlot)
    {
      int sibling = selectedWeapon;
      do
      {
        sibling = getWeaponSibling(sibling);
      }
      while (sibling != -1 && !CheckInventory(GetWeaponName(sibling)));
      if (sibling != -1)
      {
        selectedWeapon = sibling;
      }
    }
    else
    {
      SetSelectedIfAvailable(getWeaponBySlot(slotNumber));
    }
  }
}
